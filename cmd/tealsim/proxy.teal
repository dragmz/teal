#pragma version 9

txn ApplicationID
int 0
==
bnz end

// txn-1 (pay)
// txn (app call)
// txn+1 (app creation)

    txn GroupIndex
    int 1
    +
    store 0 // txn+1 index

    txn NumAppArgs
    store 1 // num args (call_n)

    load 0
    gtxns NumAppArgs
    store 3 // num args (create_n)

itxn_begin

    int ApplicationCall
    itxn_field TypeEnum

    load 0
    gtxns LocalNumByteSlice
    itxn_field LocalNumByteSlice

    load 0
    gtxns LocalNumUint
    itxn_field LocalNumUint

    load 0
    gtxns GlobalNumByteSlice
    itxn_field GlobalNumByteSlice

    load 0
    gtxns GlobalNumUint
    itxn_field GlobalNumUint

    load 0
    gtxns ApprovalProgram
    itxn_field ApprovalProgram

    load 0
    gtxns ClearStateProgram
    itxn_field ClearStateProgram

    int 0
    store 2 // i = 0

    loop_create_args:
    
        load 3
        load 2
        ==
        bnz submit_create // if(i == create_n) goto submit

        load 0
        load 2
        gtxnsas ApplicationArgs
        itxn_field ApplicationArgs // append(args, arg[i])

        load 2
        int 1
        +
        store 2 // i = i + 1

        b loop_create_args

submit_create:
itxn_submit

itxn_begin

    int ApplicationCall
    itxn_field TypeEnum

    itxn CreatedApplicationID
    itxn_field ApplicationID

    int 0
    store 2 // i = 0

    loop_call_args:
    
        load 1
        load 2
        ==
        bnz submit_call // if(i == call_n) goto submit

        load 2
        txnas ApplicationArgs
        itxn_field ApplicationArgs // append(args, arg[i])

        load 2
        int 1
        +
        store 2 // i = i + 1

        b loop_call_args
    submit_call:

itxn_submit

end:
int 1